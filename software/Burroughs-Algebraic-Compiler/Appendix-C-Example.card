2 COMMENT
2     SAMPLE PROGRAM FROM APPENDIX C (PAGE 34) IN THE PROGRAMMERS MANUAL
2     OF THE BURROUGHS ALGEBRAIC COMPILER FOR THE 205, PRELMINARY
2     EDITION, BULLETIN 3041, FEBRUARY 1961, BURROUGHS CORPORATION,
2     PASADENA, CALIFORNIA$
2
2 BEGIN PROCEDURE SIMPS(A, B, EPSILON, BOUND$ VALUE$ F())$
2   BEGIN COMMENT
2       A,B ARE LIMITS OF INTEGRATION. EPSILON IS PERMISSIBLE DIFFERENCE
2       BETWEEN TWO SUCCESSIVE SUMS. BOUND IS UPPER BOUND FOR ABS(F(X))
2       IN THE INTERVAL (A,B). VALUE IS THE ANSWER. F() IS THE FUNCTION
2       TO BE INTEGRATED BY SIMPSONS RULE$
2   TH = B-A$
2   IBAR = BOUND.TH$
2   N = 1$
2   J = 0.5(F(A)+F(B))TH$
2
2 LOOP..
2   H = 0.5TH$
2   S = 0$
2   FOR K = (A+H, TH, B)$
2   S = S+F(K)$
2   I = J+4H.S$
2   IF ABS(I-IBAR) LEQ EPSILON$
2     BEGIN
2     VALUE = I/3$
2     RETURN
2     END$
2
2   IBAR = I$
2   J = 0.25(I+J)$
2   N = N+N$
2   TH = H$
2   GO TO LOOP
2   END
2 END$
2
2 BEGIN PROCEDURE DARCTAN(X)$
2   BEGIN
2   DARCTAN() = 1/(X*2+1)$
2   RETURN
2   END
2 END$
2
2 COMMENT NOW THE PROGRAM PROPER FOLLOWS$
2
2 SIMPS(0.0, 1.0, 1**-5, 2.0$ S$ DARCTAN())$
2 WRITE($$ A, B)$
2 STOP$
2 OUTPUT A(S)$
2 FORMAT B(X11.8, T1)$
2 FINISH$
